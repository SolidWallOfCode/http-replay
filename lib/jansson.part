Import('*')
PartName("jansson")
PartVersion(GitVersionFromTag("2.12.0.dev"))

# generate the build files
env.Command(
    ["Makefile"],
    ["${CHECK_OUT_DIR}/CMakeLists.txt"],
    "cd ${TARGET.dir} ; cmake ${SOURCE.dir.abspath}\
     -DJANSSON_BUILD_SHARED_LIBS=TRUE\
     -DJANSSON_BUILD_DOCS=OFF\
     -DCMAKE_INSTALL_PREFIX=${ABSPATH('#_makeinstall')}/${PART_NAME}\
     -DCMAKE_INSTALL_LIBDIR=lib\
     -DCMAKE_BUILD_TYPE=Release"
)

# build the code
env.Command(
    [
        "#_makeinstall/${PART_NAME}/lib/libjansson.so.4.11.1",
        env.FileSymbolicLink("#_makeinstall/${PART_NAME}/lib/libjansson.so.4"),
        env.FileSymbolicLink("#_makeinstall/${PART_NAME}/lib/libjansson.so"),
        "#_makeinstall/${PART_NAME}/include/jansson_config.h",
        "#_makeinstall/${PART_NAME}/include/jansson.h",
        "#_makeinstall/${PART_NAME}/lib/pkgconfig/jansson.pc",
        "#_makeinstall/${PART_NAME}/lib/cmake/jansson/janssonConfig.cmake",
        "#_makeinstall/${PART_NAME}/lib/cmake/jansson/janssonConfigVersion.cmake",
        "#_makeinstall/${PART_NAME}/lib/cmake/jansson/janssonTargets.cmake",
        "#_makeinstall/${PART_NAME}/lib/cmake/jansson/janssonTargets-release.cmake",
    ],
    ["Makefile"],
    [
        "cd ${{SOURCE.dir}} ; cmake --build . --config Release --target install -- VERBOSE=1 CC=${{CC}} CXX=${{CXX}} $(-j{jobs}$)".format(
            jobs=env.GetOption('num_jobs'))
    ],
    allow_duplicates=True
)

# define the symlinks we will have created

env.InstallBin(
    env.SetRPath(
        env.Glob("#_makeinstall/${PART_NAME}/bin/*")
    )
)
env.InstallLib(env.Glob("#_makeinstall/${PART_NAME}/lib/*.a"))
env.InstallLib(
    env.SetRPath(
        env.Glob("#_makeinstall/${PART_NAME}/lib/*.so*")
    )
)
env.InstallInclude(
    env.SdkInclude(
        env.Glob("#_makeinstall/${PART_NAME}/include/brotli/*"),
        sub_dir="brotli",
        add_to_path=False
    ),
    sub_dir="brotli"
)
substdict = {env.subst("${ABSPATH('#_makeinstall/$PART_NAME')}"):env.subst("${PACKAGE_ROOT}")}
pc_files = [
    env.Substfile(target="${SOURCE.file}", source=f, SUBST_DICT=substdict) for f in env.Glob("#_makeinstall/${PART_NAME}/lib/pkgconfig/*.pc")
    ]
env.InstallLib(pc_files, auto_add_libs=False, sub_dir="pkgconfig", add_to_path=False)

env.ExportItem("PKGCONFIG", env.Dir("#_makeinstall/${PART_NAME}/lib/pkgconfig").abspath)
env.ExportItem("SDK", env.Dir("#_makeinstall/${PART_NAME}").abspath)
